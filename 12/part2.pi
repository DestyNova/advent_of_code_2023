import util.

main =>
  Data = parse_all(read_file_lines()),
  println(sum(Data.map(calc))).

table
calc({[],Lengths}) = cond(Lengths.len > 0, 0, 1).  % should have no remaining sequences
calc({['.'|Xs],Lengths}) = calc({Xs,Lengths}).     % safe to consume a .
calc({Xs@['#'|_],[]}) = 0.                         % already consumed all sequences: invalid
calc({Xs@['?'|_],[]}) = calc({Xs.tail,[]}).        % no remaining sequences: this must be a .
calc({Xs@['#'|_],Lengths}) = count_broke(Xs, Lengths).
calc({Xs@['?'|_],Lengths}) = count_broke(Xs, Lengths) + calc({Xs.tail, Lengths}). % both possibilities

count_broke(Xs, Lengths) = R =>
  L = Lengths[1],
  Block = Xs.take(L).remove_dups(),
  R = cond((Xs.len < L || membchk('.', Block) || (Xs.len > L && Xs[L+1] == '#')),
    0,                                             % mismatched sequence length
    calc({Xs.drop(L+1),Lengths.tail})).            % valid sequence, drop it and move on

parse_all([]) = [].
parse_all([S|Xs]) = R =>
  [Springs,Ns] = S.split(),
  Lengths = Ns.split(",").map(parse_term),
  Megasprings = join([Springs : _ in 1..5], '?'),
  R = [{Megasprings, flatten([Lengths : _ in 1..5])}|parse_all(Xs)].

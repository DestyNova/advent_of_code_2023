import util.

main =>
  G = read_file_lines(),

  N = 1000000000,
  I = 0,
  Unroll_size = 1000,

  while (I < N)
    Steps = min(Unroll_size, N - I),
    G := G.cycle(Steps),
    I := I + Steps,
  end,
  println(calc_load(G)).

table
cycle(G, Unroll_size) = R =>
  foreach (I in 1..Unroll_size)
    G := G.move(north).move(west).move(south).move(east)
  end,
  R = G.

table(+,+)
move(Grid,Dir) = R =>
  G = Grid.copy_term,
  foreach (J in 1..G.len)
    foreach (I in 1..G[1].len)
      {Row_0,Col_0} = transform(G.len,J,I,Dir),
      if G[Row_0,Col_0] == 'O' then
        Y = J,
        {Row,Col} = transform(G.len,Y,I,Dir),
        G[Row,Col] := '.',
        {Row1,Col1} = transform(G.len,Y-1,I,Dir),
        while (Y > 1 && G[Row1,Col1] == '.')
          Y := Y - 1,
          {Row_t,Col_t} = transform(G.len,Y-1,I,Dir),
          Row1 := Row_t,
          Col1 := Col_t,
        end,
        {Row2,Col2} = transform(G.len,Y,I,Dir),
        G[Row2,Col2] := 'O',
      end,
    end,
  end,
  R = G.

transform(L,J,I,north) = {J,I}.
transform(L,J,I,east) = {I,L-J+1}.
transform(L,J,I,south) = {L-J+ 1,I}.
transform(L,J,I,west) = {L-I+1,J}.

calc_load(G) = R =>
  R = 0,
  foreach (I in 1..G.len)
    R := R + (G.len - I + 1) * sum([1 : C in G[I], C == 'O']),
  end.

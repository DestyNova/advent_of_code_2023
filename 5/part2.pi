import util.
import sat.

main =>
  M = new_map(),
  extract(read_file_lines(), M, false),

  Ranges = chunks_of(M.get("seeds"),2),
  Num_ranges = Ranges.len,

  Seeds = [],
  Locs = [],

  Min = 2**34,
  I = 0,
  foreach ([Start,Len] in Ranges)
    I := I + 1,
    Seed :: 1..2**34,
    Seed #>= Start,
    Seed #< Start+Len,
    Loc :: 1..2**34,
    printf("Solving range %d/%d: %w %w...\n", I, Num_ranges, Start, Len),
    lookup(Seed,"seed",M,Loc),
    solve([seq,$min(Loc),$report(printf("Best so far: %d\n",Loc))],[Seed,Loc]),
    printf("Seed: %w, location: %w\n", Seed, Loc),
    if Loc < Min then
      Min := Loc
    end,
    Seeds := [Seed|Seeds],
    Locs := [Loc|Locs],
  end,
  println(zip(Seeds,Locs).reverse),
  println(min=Min).

lookup(N,"location",M,R) =>
  N #= R.

lookup(N,Src,M,R) =>
  {Dest,Mappings} = M.get(Src),

  Ds = new_list(Mappings.len),
  Ds :: 0..2**34,
  I = 1,
  foreach ([Dstart,Sstart,L] in Mappings)
    Ds[I] #= cond(Sstart #<= N #/\ Sstart+L #> N, N + Dstart - Sstart, 0), % 0 -> not covered by range
    Ds[I] #>= 0,
    I := I + 1,
  end,

  Max #= max(Ds),
  Max :: 0..2**34,
  D #= cond(Max #= 0, N, Max),
  lookup(D,Dest,M,R).

extract([],M,_).
extract([""|Rest],M,CurrentMap) :- extract(Rest,M,CurrentMap).
extract([[s,e,e,d,s,':',' '|Xs]|Rest],M,_) :-
  M.put("seeds", Xs.split.map(parse_term)),
  extract(Rest,M,false).

extract([S|Rest],M,_) :-
  membchk(':',S),
  [X|_] = S.split,
  [Src,_,Dest] = X.split("-"),
  extract(Rest,M,{Src,Dest}).

extract([S|Rest],M,{Src,Dest}) :-
  {_,Ranges} = M.get(Src,{Dest,[]}),
  M.put(Src, {Dest,[S.split.map(parse_term)|Ranges]}),
  extract(Rest,M,{Src,Dest}).
